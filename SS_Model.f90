
SUBROUTINE SS_MODEL(CTRL,FREQ,TETAD,Y,TB_UBC,AUX_SOIL,TB_OUT,SCOPT,&
                OPT_DMAXTOPEX)
!Cited 
!Call SS_Model(Ctrl,Freq,Angle,Profile,Tb_Bc,Xin,Tb,ScatOpt,Opt_DmaxToPex)

!LIST OF ALL THE RELATED FUNCTIONS:
!ABSCOEFF.f90
!FRESNELRC.f90
!PFADI.f90
!RUFFSOIL.f90
!SCCOEFF.f90


! -----------------------------------------------------------------------------
!
!     SOIL AND SNOW RADIATIVE TRANSFER MODEL
!
!     FORTRAN 90 VERSION OF MEMLS PROGRAM (ORIGINALLY WRITTEN IN MATLAB BY 
!     WIESMANN AND MATZLER TO COMPUTE BRIGHTNESS TEMPERATURES OF N-LAYERED
!     SNOWPACKS) INTEGRATED WITH A SOIL RADIATIVE TRANSFER SCHEME.  THIS 
!     VERSION OF THE CODE IS SET UP TO PERFORM CALCULATIONS BASED ON DATA FROM
!     A SINGLE SNOWPACK AT THE FREQUENCIES AND ANGLES SPECIFIED IN THE INPUTS.  
!     IF THE NUMBER OF LAYERS SPECIFIED IN CTRL(1) IS ZERO, THEN THIS CODE
!     WILL COMPUTE THE BARE SOIL BRIGHTNESS TEMPERATURE
!
!     IN THE CURRENT VERSION, EACH SNOWPACK MUST HAVE THE SAME NUMBER OF LAYERS.
!
! ----------------------------------------------------------------------------
!
!     DESCRIPTION OF INPUTS AND OUTPUTS (SEE JOURNAL ENTRY 19 SEP 05)
!
!     THE CTRL VECTOR SPECIFIES 5 OVERALL RUN CONTROLS, CTRL=(N_LYRS, N_POLARIZATION,
!       N_SNOWPARAMETERS,N_FREQ,OPT_DMAX2PEX), WHERE OPT_DMAXTOPEX REFERS TO WHICH
!       METHOD IS USED TO CONVERT DMAX2PCI.
!
!     THE FREQ AND TETAD VECTORS SPECIFY AT WHICH FREQUENCIES CALCULATIONS 
!       ARE TO BE DONE.
!
!     THE Y ARRAY SPECIFIES SNOW INPUTS.  THERE IS ONE ROW FOR EACH SNOW
!       LAYER SPECIFIED IN CTRL(1), ONE COLUMN SPECIFIED FOR EACH SNOW
!       VARIABLE,IN ORDER: LAYER THICKNESS [M], LAYER DENSITY [KG/M3],
!       LAYER GRAIN DIAMETER [M], LAYER LIQUID WATER CONTENT [FRAC],
!       LAYER TEMPERATURE [K]. ROWS ARE FOR SNOW LAYERS FROM TOP TO BOTTOM.
!
!     THE TB_UBC ARRAY SPECIFIES THE UPPER BOUNDARY CONDITION BRIGHTNESS
!       TEMPERATURE (SKY, VEGETATION, COSMIC).  IT HAS TWO ROWS (FOR H AND V
!       POLARIZATION) AND CTRL(4) COLUMNS: ONE FOR EACH FREQUENCY.
!
!     THE AUX_SOIL ARRAY SPECIFIES 8 AUXILIARY INPUTS, BASICALLY FOR SOIL.
!       THESE INCLUDE (IN ORDER): N_LYRS, SOIL TEMPERATURE [K], SOIL VOLUMETRIC
!       WATER CONTENT [FRAC],SOIL_ROUGHNESS [M], SOIL DENSITY [KG/M3], 
!       SOIL SAND CONTENT [%], SOIL SILT CONTENT [%], SOIL CLAY CONTENT [%]. 
!       WHEN N_LYRS IS 0, IT MEANS IT IS BARE SOIL (NO SNOW) IN THIS SCENARIO.
!
!     THE TB_OUT ARRAY CONTAINS THE CALCULATED BRIGHTNESS TEMPERATURE OUTPUTS
!       AT EACH POLARIZATION (ROWS) AND FREQUENCY (COLUMNS).
!
!     NOTE: JINMEI (MAY18,2015), THE CTRL VECTORS AND THE AUX_SOIL (ORIGIANLLY 
!       CALLED AUX_INS) ARRAY HAVE BEEN REVISED SLIGHTLY TO SAVE DIFFERENT PARAMS
!       FROM LAST VERSION OF MCMCRUN CODE.
!
! ----------------------------------------------------------------------------
!
!     1.DESCRIPTION.  THE CODE IS A TRANSLATION OF THE MEMLS2 CODE RECEIVED 
!     THROUGH CORRESPONDENCE WITH MATZLER AND A SOIL RADIATIVE TRANSFER
!     MODEL RECEIVED THROUGH CORRESPONDENCE WITH PULLIAINEN SOMETIME IN 2003.
!     THE MEMLS CODE USES THE GROUND REFLECTIVITY AND THE SNOWPACK DATA TO 
!     COMPUTE THE BRIGHTNESS TEMPERATURE OF THE SNOWPACK. THE CODE WAS 
!     TRANSLATED TO FORTRAN IN ORDER  TO SAVE COMPUTATION TIME IN A DATA 
!     ASSIMILATION SCHEME IN WHICH MILLIONS OF CALLS MUST BE MADE TO THE 
!     RADIATIVE TRANSFER MODEL .  IN TESTS, ONE HUNDRED BRIGHTNESS TEMPERATURE
!     CALCULATIONS TOOK TEN TIMES LESS PROCESSOR TIME IN THIS FORTRAN VERSION 
!     THAN IN THE ORIGINAL MATLAB.  EXTENSIVE VERIFICATION WAS PERFORMED USING
!     SNOWPIT DATA FOR NINE DAYS IN ONE WINTER DURING THE CLPX PROGRAM AS WELL
!     AS 3-LAYER SNOWMODEL RESULTS FROM THE MODIFIED SAST (SUN ET AL, 99 IN 
!     IN JGR) + SSIB MODEL RUN AT MAMMOTH MOUNTAIN, TO ENSURE THAT THE FORTRAN
!     CODE PRODUCES IDENTICAL RESULTS AS THE MATLAB CODE.
!
!     2. MODIFICATION.  THE MAIN CHANGE MADE TO THE MEMLS CODE IN THE 
!     TRANSLATION PROCESS WAS THAT THE VAN POLDER APPROXIMATION (EFFECTIVE 
!     MIXING THEORY) WAS  NO LONGER BEING USED IN THE MATLAB CODES WHEN I 
!     RECEIVED THEM.  IN FACT, THE REAL PART OF THE SNOW DIELECTRIC WAS USED 
!     INSTEAD IN SEVERAL SUBROUTINES. THIS WAS SLIGHTLY INCONSISTENT WITH THE 
!     PAPERS, SO (FOLLOWING MATZLER'S ADVICE VIA EMAIL) I WROTE A NEW 'POLDER' 
!     SUBROUTINE WHICH USES THE NEWTON-RAPHSON APPROXIMATION TO SOLVE FOR THE 
!     SNOW EFFECTIVE PERMITTIVITY - SEE MATZLER'S 1996 PAPER IN IEEE FOR THE 
!     EQUATIONS AND THE SUBROUTINE COMMENTS BELOW.  ACCORDING TO COMPARISON 
!     WITH THE SNOWPIT DATA RESULTS, THE EFFECT OF USING THE SNOW DIELECTRIC 
!     INSTEAD OF EFFECTIVE PERMITTIVITY ON THE BRIGHTNESS TEMPREATURE WAS 
!     MINIMAL (~0.1 DEGREE).  HOWEVER, BECAUSE I COULD NOT TEST WITH A MORE 
!     EXTENSIVE DATASET, I LEFT THE SLIGHTLY MORE COMPUTATIONALLY EXPENSIVE 
!     EFFECTIVE PERMITTIVITY CALCULATION IN PLACE FOR THE SAKE OF INTERNAL 
!     CONSISTENCY.
!
!     JINMEI'S REVISION (MAY18,2015): NOW THE SNOW DIELECTRIC CONSTANTS FOLLOWS 
!     MEMLS3 - THE NEWEST VERSION OF MEMLS
!
!     ANOTHER CHANGE THAT I MADE TO MEMLS WAS TO SET UP THE PROGRAM TO PASS IN 
!     SNOW GRAIN DIAMETER SINCE THIS IS USUALLY WHAT IS MEASUSRED IN SNOWPITS 
!     AND MODELED BY PROGNOSTIC EQUATIONS IN SNOW MODELS.  THE CONSTANT OF 
!     PROPORTIONALITY BETWEEN THE GRAIN DIAMETER AND THE CORRELATION LENGTH (SEE
!     MATLZER, 2002 IN JOURNAL OF GLACIOLOGY) IS ALSO PASSED IN.  
!
!     A THIRD CHANGE I MADE TO MEMLS WAS TO USE A SWITCH TO DETERMINE WHETHER 
!     THE BORN APPROXIMATION OR THE EMPIRICAL SCATTERING COEFFICIENT WOULD BE 
!     USED.  IF THE MAXIMUM CORRELATION LENGTH IN THE SNOWPACK IS GREATER THAN 
!     0.33 MM, THE BORN APPROXIMATION IS USED; OTHERWISE, THE EMPIRICAL FORMULA
!     IS USED.
!
!     HISTORY
!       1.0  - ORIGINAL TRANSLATION - MD 11/2005          
!       2.0  - ADDED SUPPORT FOR SUCCESSIVE COHERENT LAYERS - MD & JG 4/2011
!       3.0  - ADDED DOBSON MODEL TO SIMULATE SOIL DIELECTRIC CONSTANTS



IMPLICIT NONE

! 0.  PRELIMINARIES
!  A. DECLARATIONS
!   0.INPUTS AND OUTPUTS
INTEGER,INTENT(IN) :: CTRL(4),SCOPT,OPT_DMAXTOPEX
REAL,INTENT(IN) :: AUX_SOIL(8),TB_UBC(CTRL(2),CTRL(4)),FREQ(CTRL(4)),TETAD(CTRL(4))
REAL,INTENT(IN) :: Y(CTRL(1),CTRL(3))
REAL,INTENT(OUT):: TB_OUT(CTRL(2),CTRL(4)) !Modified to run both polarizations, Jinmei2015

!   1.FOR PART 1
REAL, DIMENSION(:), ALLOCATABLE :: TI,WIFR,ROIKG,DI,GDI,&
ROI,GDIMM,PCI,EPSI,EPSII,GAI,NS,TEI,DEI,SIH,SIV
REAL,DIMENSION(:), ALLOCATABLE :: XNUM,XROI,XEPSI,XEPSII,XTEI,&
XSIH,XSIV,XDI,XDEI,XTI,XPCI,XWIFR,XGAI
REAL,DIMENSION(:), ALLOCATABLE :: RROI,REPSI,REPSII,RTEI,RSIH,&
RSIV,RDI,RDEI,RTI,RPCI,RWIFR,RGAI
REAL MJU0,EPS0,PI,TSKYH,TSKYV,TETA
REAL MAXPCI !MAXIMUM PCI TO DETERMIN USING EMPIRICAL OR BORN APPROXIMATION
REAL GND_TEMP,GND_MV,GND_SIG,SOIL_ROIKG,SAND,SILT,CLAY,GND_EPSI,GND_EPSII !SOIL PARAMETERS IN AUX_SOIL
REAL ESG_H,ESG_V
INTEGER SCCHO,I,NUM,NFREQ,K,RNUM


!   2.FOR PART 2
REAL EPS_T,EPS_B,TETAD_SOIL
COMPLEX, DIMENSION(:), ALLOCATABLE :: EPSR
COMPLEX EPS_UPPER
REAL KSIG,S0H,S0V

!   3.FOR PART 3
REAL,DIMENSION(:),ALLOCATABLE :: GBIH,GBIV,GS6,GA2I,TSCAT,&
  RSIHLONG,RSIVLONG

!   4.FOR PART 4
REAL,DIMENSION(:),ALLOCATABLE :: RI,TRI,DH,DV !TRI IS T MATRIX OF MEMLS


!  B. CONSTANTS AND CONTROL STATEMENTS
MJU0=1.2566e-006
EPS0=8.8542e-012
PI=3.14159
NFREQ=CTRL(4)  ! THIS CODE LOOPS OVER THE NUMBER OF FREQUENCIES FOR ONE SET
               ! OF SNOW PROPERTIES.
NUM=CTRL(1) ! NUMBER OF LAYERS IN THE SNOWPACK


!  C. ALLOCATE AND EXTRACT STATEMENTS
ALLOCATE(DI(1:NUM),ROIKG(1:NUM),GDI(1:NUM),WIFR(1:NUM),TI(1:NUM))

DI(1:NUM)=   Y(1:NUM,1)
ROIKG(1:NUM)=Y(1:NUM,2)
GDI(1:NUM)=  Y(1:NUM,3)
WIFR(1:NUM)= Y(1:NUM,4)
TI(1:NUM)=   Y(1:NUM,5)


GND_TEMP=AUX_SOIL(2)
GND_MV=AUX_SOIL(3)
GND_SIG=AUX_SOIL(4)
SOIL_ROIKG=AUX_SOIL(5)
SAND=AUX_SOIL(6)
SILT=AUX_SOIL(7)
CLAY=AUX_SOIL(8)



!  1. CHECK TO MAKE SURE THERE IS SNOW.  OTHERWISE DEAL WITH SOIL ONLY
!     NOTE: AUX_SOIL(1) CONTAINS 0 IN THE CASE OF NO SNOW, THOUGH NUM
!     WILL BE 1 TO AVOID INCORRECT ARRAY ALLOCATIONS

!IF(NUM.EQ.2)Then
!PRINT *,'AUX_SOIL',AUX_SOIL
!End If


IF (NUM.EQ.0) THEN

  DO K=1,NFREQ
    ! THERE IS NO SNOWPACK, SO COMPUTE REFLECTIVITY OF GROUND AND OBTAIN
    ! OUTPUT BRIGHTNESS TEMPERATURES
    TETAD_SOIL=TETAD(K)
    EPS_UPPER=1.0000 !DIELECTRIC PERMITTIVITY OF AIR

    KSIG=REAL(2*PI*FREQ(K)*1E9*(MJU0*EPS0*EPS_UPPER)**0.5)*GND_SIG
    CALL RUFFSOIL(FREQ(K)*1E9,GND_MV,GND_TEMP,KSIG,GND_SIG,TETAD_SOIL,&
      EPS_UPPER,SOIL_ROIKG,SAND,SILT,CLAY,S0H,S0V)

    TSKYH=TB_UBC(1,K)
    TSKYV=TB_UBC(2,K)

    IF (CTRL(2).EQ.1) THEN !Vertical only! No horizontal only allowed...
      TB_OUT(1,K)=(1-S0V)*GND_TEMP+S0V*TSKYV
    ELSEIF (CTRL(2).EQ.2) THEN
      TB_OUT(1,K)=(1-S0V)*GND_TEMP+S0V*TSKYV
      TB_OUT(2,K)=(1-S0H)*GND_TEMP+S0H*TSKYH
    END IF
  END DO

  Deallocate(DI,ROIKG,GDI,WIFR,TI)

  RETURN !TO MAIN PROGRAM
END IF




! 2. COMPUTE BRIGHTNESS TEMPERATURE OF SNOWPACK AND SOIL
DO K=1,NFREQ

  !  A. PRELIMINARIES
  ! A.1. CHOOSE THE WAY TO CONVERT FROM DI TO PEX USED BY MEMLS
  ALLOCATE(ROI(1:NUM),GDIMM(1:NUM),PCI(1:NUM))
  GDIMM=GDI
  ROI=ROIKG/1000
  IF(OPT_DMAXTOPEX==1) THEN
     DO I=1,NUM
       IF(ROIKG(I).GT.183.4 .AND. GDIMM(I).GT.0.125) THEN
          PCI(I)=0.18+0.09*LOG(GDIMM(I))
       ELSE
          PCI(I)=0.05
       ENDIF
     ENDDO
  ELSEIF (OPT_DMAXTOPEX==2) THEN
     DO I=1,NUM
       IF(ROIKG(I).GT.146.7 .AND. GDIMM(I).GT.0.5) THEN
          PCI(I)=0.227+0.126*LOG(GDIMM(I))
       ELSE
          PCI(I)=0.09
       ENDIF
     ENDDO
  ELSE
     IF(OPT_DMAXTOPEX==3) THEN
        PCI=GDIMM !Use this if your input is already PEX
     ELSE
        PRINT *, 'PLEASE CHOOSE A SUITABLE WAY (1-3) TO CALCUCLATE PEX'
        PCI=GDIMM
     ENDIF
  ENDIF



  !  A.2. ALLOCATE AND UNIT CONVERSION STATEMENTS

  !  A.3. DETERMINE WHICH SCATTERING COEFFICIENT COMPUTATION METHOD TO
  !  USE THE VALUE OF 0.33 USED HERE FROM PERSONAL CORRESPONDENCE WITH
  !  MATZLER 

!  MAXPCI=MAXVAL(PCI)
!  IF (MAXPCI<0.33) THEN
!    SCCHO=1 ! USE EMPIRICAL SCATTERING COEFFICIENT FROM W&M 99
!  ELSE 
!    SCCHO=2 ! USE BORN APPROXIMATION FROM MATZLER AND WIESMANN 99
!  ENDIF

!THIS OPTION OUGHT TO BE AUTOMATED:
!DURING MY WORK WITH SSIB3+MEMLS TO MODEL GBMR-7 TB, I NEEDED TO USE SCCHO=2
!SCCHO=2
!IN ORDER TO SAVE TIME FOR THE SYNTHETIC TESTS, HOWEVER, I USED SCCHO=1
!SCCHO=1

  IF (SCOPT .EQ. 1) THEN
    SCCHO=1
  ELSEIF  (SCOPT .EQ. 2) THEN
    SCCHO=2
  ELSEIF  (SCOPT .EQ. 3) THEN
    MAXPCI=MAXVAL(PCI)
    IF (MAXPCI<0.33) THEN
      SCCHO=1 ! USE EMPIRICAL SCATTERING COEFFICIENT FROM W&M 99
    ELSE
      SCCHO=2 ! USE BORN APPROXIMATION FROM MATZLER AND WIESMANN 99
    ENDIF
  ELSE
    PRINT *, 'SS_MODEL: INVALID SCATTERING OPTION, USING EMPIRICAL'
    SCCHO=1
  END IF


  ! B.  COMPUTE RADIATIVE TRANSFER PROPERTIES OF SNOW

  ALLOCATE(EPSI(1:NUM),EPSII(1:NUM),GAI(1:NUM),NS(1:NUM),&
    TEI(1:NUM+1),DEI(1:NUM),SIH(1:NUM),SIV(1:NUM),XROI(1:NUM),&
    XEPSI(1:NUM),XEPSII(1:NUM),XTEI(1:NUM+1),XSIH(1:NUM),&
    XSIV(1:NUM),XDI(1:NUM),XDEI(1:NUM),XTI(1:NUM),XPCI(1:NUM),&
    XWIFR(1:NUM),XGAI(1:NUM))

  CALL RO2EPSD(ROI,TI,FREQ(K),EPSI,EPSII,NUM)

  CALL MIXMOD(FREQ(K),TI,WIFR,EPSI,EPSII,NUM)

  CALL ABSCOEFF(EPSI,EPSII,TI,FREQ(K),WIFR,GAI,NUM)


  TETA=TETAD(K)*PI/180
  NS=EPSI**0.5
  TEI(1:NUM)=ASIN(SIN(TETA)/NS)
  TEI(NUM+1)=TETA
  CALL PFADI(TEI,DI,DEI,NUM)
  CALL FRESNELC(TEI,EPSI,SIH,SIV,NUM)


  CALL SLRED(NUM,ROI,EPSI,EPSII,TEI,SIH,SIV,DI,DEI,TI,PCI,WIFR,&
    GAI,FREQ(K),RNUM,XROI,XEPSI,XEPSII,XTEI,XSIH,XSIV,XDI,XDEI,&
    XTI,XPCI,XWIFR,XGAI)


  ALLOCATE(RROI(RNUM),REPSI(RNUM),REPSII(RNUM),&
    RTEI(RNUM+1),RSIH(RNUM),RSIV(RNUM),RDI(RNUM),&
    RDEI(RNUM),RTI(RNUM),RPCI(RNUM),RWIFR(RNUM),&
    RGAI(RNUM))

  DO I=1,RNUM
    RROI(I)=XROI(I)
    REPSI(I)=XEPSI(I)
    REPSII(I)=XEPSII(I)
    RSIH(I)=XSIH(I)
    RSIV(I)=XSIV(I)
    RDI(I)=XDI(I)
    RDEI(I)=XDEI(I)
    RTI(I)=XTI(I)
    RPCI(I)=XPCI(I)
    RWIFR(I)=XWIFR(I)
    RGAI(I)=XGAI(I)
  END DO
  DO I=1,RNUM+1
    RTEI(I)=XTEI(I)
  END DO


  ! C.  COMPUTE GROUND REFLECTIVITIES

  ALLOCATE(EPSR(1:RNUM))

  EPS_T=1.0
  TETAD_SOIL=TETAD(K)

  DO I=RNUM,1,-1
    EPS_B=EPSI(I)
    CALL AOT(EPS_T,EPS_B,TETAD_SOIL)
    EPS_T=EPS_B
  END DO

  EPSR=CMPLX(REPSI,(-1*REPSII)) 
  EPS_UPPER=EPSR(1)


  KSIG=REAL(2*PI*FREQ(K)*1E9*(MJU0*EPS0*EPS_UPPER)**0.5)*GND_SIG

  !Print *,'EPS_UPPER',EPS_UPPER

  CALL RUFFSOIL(FREQ(K)*1E9,GND_MV,GND_TEMP,KSIG,GND_SIG,TETAD_SOIL,&
    EPS_UPPER,SOIL_ROIKG,SAND,SILT,CLAY,S0H,S0V,NUM)


  ! D.  COMPUTE SCATTERING COEFFICIENTS, REFLECTIVITIES AND 
  !     TRANSMISSIVITIES

  DEALLOCATE(RTEI,RDEI) ! BECAUSE THESE ARRAYS WILL CHANGE SIZES
  ALLOCATE(GBIH(RNUM),GBIV(RNUM),GS6(RNUM),GA2I(RNUM),&
    TSCAT(RNUM),RTEI(RNUM),RDEI(RNUM),RSIHLONG(RNUM+1),&
    RSIVLONG(RNUM+1))
     
  CALL SCCOEFF(RROI,RTI,RPCI,FREQ(K),RWIFR,RGAI,SCCHO,GBIH,GBIV,& 
    GS6,GA2I,RNUM,REPSI,REPSII)


  CALL PFADC(TETA,RDI,REPSI,GS6,RDEI,RTEI,TSCAT,RNUM)

  RSIHLONG(1)=S0H
  RSIHLONG(2:RNUM+1)=RSIH
  RSIVLONG(1)=S0V
  RSIVLONG(2:RNUM+1)=RSIV


  CALL POLMIX(TSCAT,RSIHLONG,RSIVLONG,RNUM)


  ! E.  COMPUTE BRIGHTNESS TEMPERATURES AND EMISSIVITIES OF SNOWPACK AND 
  !       GROUND

  ALLOCATE(RI(1:RNUM),TRI(1:RNUM),DH(1:RNUM),DV(1:RNUM))

  ! Jinmei 2/11/2014, repair an error when using only vertical polarization
  !the new codes are
  IF (CTRL(2).EQ.1) THEN
    TSKYV=TB_UBC(1,K)
    TSKYH=TSKYV  !not used actually
  ELSEIF (CTRL(2).EQ.2) THEN
    TSKYV=TB_UBC(1,K)
    TSKYH=TB_UBC(2,K)
  END IF

  CALL RT(GA2I,GBIH,DEI,RI,TRI,RNUM)

  CALL LAYER(RI,RSIHLONG,TRI,RTI,GND_TEMP,TSKYH,DH,RNUM)

  CALL RT(GA2I,GBIV,RDEI,RI,TRI,RNUM)

  CALL LAYER(RI,RSIVLONG,TRI,TI,GND_TEMP,TSKYV,DV,RNUM)

  !

  IF (CTRL(2).EQ.1) THEN
    TB_OUT(1,K)=(1-RSIVLONG(RNUM+1))*DV(RNUM)+RSIVLONG(RNUM+1)*TSKYV
  ELSEIF (CTRL(2).EQ.2) THEN
    TB_OUT(1,K)=(1-RSIVLONG(RNUM+1))*DV(RNUM)+RSIVLONG(RNUM+1)*TSKYV
    TB_OUT(2,K)=(1-RSIHLONG(RNUM+1))*DH(RNUM)+RSIHLONG(RNUM+1)*TSKYH
  END IF

  ! CALL EMISSIVITY(GA2I,GBIH,GBIV,RDEI,RSIHLONG,RSIVLONG,GND_TEMP,RTI,&
  !   ESG_H,ESG_V,RNUM)


! WHETHER TO CHECK THE CODE OR NOT
IF(NUM.eq.2 .and. .false.)THEN
!Print *,'DI',DI
!Print *,'ROIKG',ROIKG
!Print *,'GDI',GDI
!Print *,'TI',TI
Print *,'Y',Y(1,:)
Print *,Y(2,:)
Print *,'GND_TEMP',GND_TEMP
Print *,'GND_MV',GND_MV
Print *,'GND_SIG',GND_SIG
Print *,'PCI',PCI
Print *,'EPSI',EPSI
Print *,'EPSII',EPSII
Print *,'GAI',GAI
Print *,'GBIV',GBIV
Print *,'RSIHLONG',RSIHLONG
Print *,'RSIVLONG',RSIVLONG
ENDIF


  ! F.  DEALLOCATE VARIABLE SPACE


  DEALLOCATE(ROI,GDIMM,PCI)

  DEALLOCATE(EPSI,EPSII,GAI,NS,TEI,DEI,SIH,SIV)
  DEALLOCATE(XROI,XEPSI,XEPSII,XTEI,XSIH,XSIV,XDI,XDEI,XTI,XPCI,XWIFR,XGAI)
  DEALLOCATE(RROI,REPSI,REPSII,RTEI,RSIH,RSIV,RDI,RDEI,RTI,RPCI,RWIFR,RGAI)

  DEALLOCATE(EPSR,GBIH,GBIV,GS6,GA2I,TSCAT,RSIHLONG,RSIVLONG)
  DEALLOCATE(RI,TRI,DH,DV)


END DO

  DEALLOCATE(DI,ROIKG,GDI,WIFR,TI)


IF(NUM.eq.2 .and. .false.)THEN
    Print *,'TB_OUT',TB_OUT
ENDIF

END SUBROUTINE SS_MODEL





! -------------------------------------------------------------------------
!
SUBROUTINE EMISSIVITY(GA2I,GBIH,GBIV,DEI,SIHLONG,SIVLONG,GND_TEMP,TI,&
EH,EV,NUM)
!
! -------------------------------------------------------------------------
!
!  CODE ORIGINALLY OBTAINED IN MATLAB FROM MATZLER, MODIFIED BY MIKE.
!    SEE VERSION HISTORY
!
!   CALCULATES THE SCATTERING COEFFICIENT USING BORN APPROXIMATION
!
!       GA2I: ABSORPTION COEFFICIENT
!       GBIH: 2-FLUX SCATTERING COEFFICIENT, H POLARIZATION
!       GBIV: 2-FLUX SCATTERING COEFFICIENT, V POLARIZATION
!       DEI:  EFFECTIVE PATH LENGTH [M]
!       SIHLONG: LAYER INTERFACE REFLECTIVITY, H POLARIZATION
!       SIVLONG: LAYER INTERFACE REFLECTIVITY, V POLARIZATION
!       EH:   EMISSIVITY, H POLARIZATION
!       EV:   EMISSIVITY, V POLARIZATION
!
!   VERSION HISTORY:
!      1.0     MD 1 APR 05 THIS CODE WAS PART OF LMAIN.  I TRANSLATED TO
!                 FORTRAN FROM MATLAB AND MOVED IT TO A SEPARATE SUBROUTINE.
!                 COMPARE WIESMANN AND MATZLER, 99 EQN (8)
!
!   USES: RT, LAYER
!
!   COPYRIGHT (C) 1998 BY THE INSTITUTE OF APPLIED PHYSICS,
!   UNIVERSITY OF BERN, SWITZERLAND

IMPLICIT NONE

INTEGER, INTENT(IN) :: NUM
REAL,INTENT(IN) :: GA2I(NUM),GBIH(NUM),GBIV(NUM),DEI(NUM),&
SIHLONG(NUM+1),SIVLONG(NUM+1),GND_TEMP,&
TI(NUM)
REAL,INTENT(OUT) :: EH,EV
REAL RI(NUM),TRI(NUM),DH(NUM),DV(NUM),TBH0,TBH100,TBV0,TBV100,TSKY

! HORIZONTAL BRIGHTNESS TEMPERATURES UNDER DIFFERENT TSKY VALS

CALL RT(GA2I,GBIH,DEI,RI,TRI,NUM)

TSKY=0.
CALL LAYER(RI,SIHLONG,TRI,TI,GND_TEMP,TSKY,DH,NUM)
TBH0=(1-SIHLONG(NUM+1))*DH(NUM)+SIHLONG(NUM+1)*TSKY

TSKY=100.
CALL LAYER(RI,SIHLONG,TRI,TI,GND_TEMP,TSKY,DH,NUM)
TBH100=(1-SIHLONG(NUM+1))*DH(NUM)+SIHLONG(NUM+1)*TSKY

! VERTICAL BRIGHTNESS TEMPERATUERS UNDER DIFFERENT TSKY VALS

CALL RT(GA2I,GBIV,DEI,RI,TRI,NUM)

TSKY=0.
CALL LAYER(RI,SIVLONG,TRI,TI,GND_TEMP,TSKY,DV,NUM)
TBV0=(1-SIVLONG(NUM+1))*DV(NUM)+SIVLONG(NUM+1)*TSKY

TSKY=100.
CALL LAYER(RI,SIVLONG,TRI,TI,GND_TEMP,TSKY,DV,NUM)
TBV100=(1-SIVLONG(NUM+1))*DV(NUM)+SIVLONG(NUM+1)*TSKY

! COMPUTE EMISSIVITIES
EH=1-(TBH100-TBH0)/100
EV=1-(TBV100-TBV0)/100

END SUBROUTINE EMISSIVITY


